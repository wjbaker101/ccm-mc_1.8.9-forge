buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = '1.5.1-forge'
group = 'com.wjbaker.ccm'
archivesBaseName = 'custom_crosshair_mod'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_20"
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
}

mixin {
    defaultObfuscationEnv = "searge"
    add sourceSets.main, "mixins.ccm.refmap.json"
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
//    compile 'org.spongepowered:mixin:0.7.5-SNAPSHOT'

    embed('org.spongepowered:mixin:0.7.5-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes "ForceLoadAsMod": "true"
        attributes 'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
        attributes "TweakOrder": "0"
        attributes 'MixinConfigs': 'mixins.ccm.json'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }

    from {
        configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
}